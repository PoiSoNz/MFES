class Document
	/*
		Defines a document with the resources required for its print
	*/
	
	types
		public ColorType = <BlackWhite> | <Color>;
		public PaperSize = <A3> | <A4> | <A5>;
		public DocumentStatus = <NotPrinted> | <Queued> | <Printed>; -- A document is considered <NotPrinted> if it hasn't been queued yet
		
	instance variables
		public static id_count: nat := 1;
		public id: nat;
		public color: ColorType;
		public size: PaperSize;
		public inkQuantity: nat1;
		public paperQuantity: nat1;
		public price: real;
		public status: DocumentStatus := <NotPrinted>;
		public owner: [Client] := nil;

		inv if status <> <NotPrinted>
				then owner <> nil
				else true;

	operations
		public Document: ColorType * PaperSize * nat1 * nat1 ==> Document
		Document(color_, size_, inkQuantity_, paperQuantity_) ==
		(
			id := id_count;
			id_count := id_count + 1;
			color := color_;
			size := size_;
			inkQuantity := inkQuantity_;
			paperQuantity := paperQuantity_;
			price := calculatePrice(color_, size_, paperQuantity_);
			status := <NotPrinted>;
			return self
		)	
			post
				id_count = id_count~ + 1 and
				id = id_count~ and
				owner = nil and
				price > 0 and
				status = <NotPrinted>;
		
		/*
			Set document status as "queued"
			A document can only be queued if it is "not printed"
		*/		
		public queued: () ==> ()
		queued() ==
			status := <Queued>
			pre
				status = <NotPrinted>
			post
				status = <Queued>;
		
		/*
			Set document status as "printed"
			A document can only be printed if it is "queued"
		*/
		public printed: () ==> ()
		printed() ==
			status := <Printed>
			pre
				status = <Queued>
			post
				status = <Printed>;
		
		/*
			Set a client as the owner of the document
			Once a client as been assigned, it cannot be changed
		*/
		public setOwner: Client ==> ()
		setOwner(client) ==
			owner := client
			pre
				owner = nil
			post
				owner = client;
				
	functions
		/*
			Calculate a document based on its color paper size and paper quantity
		*/
		public static calculatePrice: ColorType * PaperSize * nat1 -> real
		calculatePrice(color, size, paperQuantity) ==
			sheetCost(size) * paperQuantity * colorCostFactor(color);
		
		/*
			Return the cost factor to be multiplied to the final price
			Black & White does not affect the final price, colored documents have price increased by 50%
		*/
		public static colorCostFactor: ColorType -> real
		colorCostFactor(color) ==
		(
			cases color:
				<BlackWhite> -> 1,
				<Color> -> 1.5
			end
		);
		
		/*
			Return the cost of each paper sheet depending on the paper size
		*/
		public static sheetCost: PaperSize -> real
		sheetCost(sheetSize) ==
		(
			cases sheetSize:
				<A3> -> 0.07,
				<A4> -> 0.05,
				<A5> -> 0.03
			end
		);
 
end Document