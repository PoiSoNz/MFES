class Report
	
	types
		public MalfunctionType = <Damaged> | <BlackInkEmpty> | <ColorInkEmpty> |
														<A3Empty> | <A4Empty> | <A5Empty>;
	
	instance variables
		private static id_count: nat := 1;
		private id: nat;
		private malfunction: MalfunctionType;
		private printer: Printer;
		private technician: [Technician] := nil;
		private closed: bool := false;

		--inv ;

	operations
		/*
			Create a report with no technician associated to fix it
		*/
		public Report: MalfunctionType * Printer ==> Report
		Report(malfunction_, printer_) ==
		(
			id := id_count;
			id_count := id_count + 1;
			malfunction := malfunction_;
			printer := printer_;
			return self
		)
			post
				id_count = id_count~ + 1 and
				id = id_count~ and
				technician = nil and
				closed = false;
		/*
			Create a report with a technician assigned to fix it
		*/
		public Report: MalfunctionType * Printer * Technician ==> Report
		Report(malfunction_, printer_, technician_) ==
		(
			id := id_count;
			id_count := id_count + 1;
			malfunction := malfunction_;
			printer := printer_;
			technician := technician_;
			return self
		)
			post
				id_count = id_count~ + 1 and
				id = id_count~ and
				technician <> nil and
				closed = false;
				
		public setTechnician: Technician ==> ()
		setTechnician(technician_) ==
			technician := technician_
			pre
				technician = nil
			post
				technician = technician_;
		
		public close: () ==> ()
		close() ==
			closed := true
			pre
				closed = false
			post
				closed = true;
 
end Report